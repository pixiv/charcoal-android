plugins {
    alias libs.plugins.android.library
    alias libs.plugins.kotlin
    id 'signing'
    id 'maven-publish'
    id 'org.jetbrains.dokka'
}

group = "net.pixiv.charcoal"
version = findProperty('CHARCOAL_RELEASE_VERSION') ?: System.getenv('CHARCOAL_RELEASE_VERSION') ?: ""

android {
    compileSdkVersion versions.compileSdk

    buildFeatures {
        buildConfig false
        compose true
    }

    defaultConfig {
        minSdkVersion versions.minSdk
        targetSdkVersion versions.targetSdk

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
        freeCompilerArgs += "-Xopt-in=kotlin.RequiresOptIn"
    }

    composeOptions {
        kotlinCompilerExtensionVersion "${composeCompilerVersion}"
    }
    namespace 'net.pixiv.charcoal.android.compose'
}

dependencies {
    api project(":charcoal-android")

    implementation deps.androidX.activity.compose
    implementation deps.androidX.compose.compiler
    implementation platform(deps.androidX.compose.bom)
    implementation deps.androidX.compose.ui
    implementation deps.androidX.compose.uiTooling
    implementation deps.androidX.compose.material
    implementation deps.kotlinX.serialization

    testImplementation deps.junit

    androidTestImplementation deps.androidX.test.junit
    androidTestImplementation deps.androidX.test.espresso
}

afterEvaluate {
    publishing {
        publications {
            release(MavenPublication) {
                from components.release
                artifact androidSourcesJar

                groupId group
                artifactId "charcoal-compose"
                version version

                pom {
                    name = "charcoal-android-compose"
                    description = "Design system library by pixiv"
                    url = "https://github.com/pixiv/charcoal-android"

                    licenses {
                        license {
                            name = 'The Apache License, Version 2.0'
                            url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                        }
                    }

                    developers {
                        developer {
                            id = 'kwzr'
                            name = 'Kazumasa Kawazure'
                            email = "kwzr@pixiv.co.jp"
                        }
                    }

                    scm {
                        connection = 'scm:git:github.com/pixiv/charcoal-android.git'
                        developerConnection = 'scm:git:ssh://github.com/pixiv/charcoal-android.git'
                        url = 'https://github.com/pixiv/charcoal-android/tree/main'
                    }
                }
            }
        }
    }
}

signing {
    sign publishing.publications
}

task androidSourcesJar(type: Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
}

dokkaHtml.configure {
    dokkaSourceSets {
        named("main") {
            noAndroidSdkLink.set(false)
        }
    }
}